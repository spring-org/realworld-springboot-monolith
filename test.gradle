jacoco {
    toolVersion = '0.8.6'
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

def jacocoExclude = [
        '**/Tag*.class',
        '**/*Config.class',
        '**/Q*.class',
        '**/*Api.class',
        '**/Request*',
        '**/Response*',
        '**/RealWorldApplication.class',
        '**/BaseTimeEntity.class',
        '**/*Exception.class'
]

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        // csv.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExclude)
        }))
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExclude)
        }))
    }
    violationRules {
        // 전체
        rule {
            // 해당 프로젝트의 최소 커버리지
            limit {
                minimum = 1.00
            }
        }
        // 패키지
        rule {
            element = 'PACKAGE'

            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                maximum = 1.00
            }
        }
        // 클래스
        rule {
            // rule을 체크할 단위는 Class
            element = 'CLASS'

            // 브랜치 커버리지를 최소한 90% 만족
            limit {
                // 조건문 등의 분기 수
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                maximum = 1.00
            }

            // 라인 커버리지를 최소한 80% 만족
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }

            // 빈 줄을 제외한 코드의 라인수를 최대 200라인으로 제한
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 200
            }
        }
    }
}